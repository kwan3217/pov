//Title 4 specifies is the official definition of the flag...
/*
Sec. 1. - Flag; stripes and stars on
The flag of the United States shall be thirteen horizontal stripes, 
alternate red and white; and the union of the flag shall be forty-eight
stars, white in a blue field

Sec. 2. - Same; additional stars
On the admission of a new State into the Union one star shall be added 
to the union of the flag; and such addition shall take effect on the 
fourth day of July then next succeeding such admission
*/                                                    
//... but in very little detail. Any pattern which can be described such is a
//US flag. However, President Eisenhower specified the flag in much 
//more detail for use by the military and executive branch.
//Flag pattern specified by Executive Order 10834, 21 August 1959. 
//http://www.ushistory.org/betsy/more/10834.htm

/*
 36 - Parks, Forests, and Public Property

The flag of the United States
WHEREAS the State of Hawaii has this day been admitted into the Union; and

WHEREAS section 2 of title 4 of the United States Code provides as follows: 
"On the admission of a new State into the Union one star shall be added to 
the union of the flag; and such addition shall take effect on the fourth day
 of July then next succeeding such admission."; and

WHEREAS the Federal Property and Administrative Services Act of 1949 (63 Stat.
377), as amended, authorizes the President to prescribe policies and 
directives governing the procurement and utilization of property by executive
 agencies; and

WHEREAS the interests of the Government require that orderly and reasonable 
provision be made for various matters pertaining to the flag and that 
appropriate regulations governing the procurement and utilization of national
flags and union jacks by executive agencies be prescribed:

NOW, THEREFORE, by virtue of the authority vested in me as President of the 
United States and as Commander in Chief of the armed forces of the United 
States, and the Federal Property and Administrative Services Act of 1949, as 
amended, it is hereby ordered as follows:

Part I - Design of the Flag

Section 1. The flag of the United States shall have thirteen horizontal 
stripes, alternate red and white, and a union consisting of white stars on a
field of blue.

Sec. 2. The positions of the stars in the union of the flag and in the union
jack shall be as indicated on the attachment to this order, which is hereby 
made a part of this order.

Sec. 3. The dimensions of the constituent parts of the flag shall conform to
the proportions set forth in the attachment referred to in section 2 of this
order.

Part II - Regulations Governing Executive Agencies

Sec. 21. The following sizes of flags are authorized for executive agencies:

Size	Dimensions of flag
	Hoist (width) Feet	Fly (length) Feet    [Ratio]
(1)	20.00           	38.00                [1.9]
(2)	10.00	                19.00                [1.9]
(3)	8.95	                17.00                [1.899+]
(4)	7.00	                11.00                [1.571+]
(5)	5.00	                9.50                 [1.9]
(6)	4.33	                5.50                 [1.270+]
(7)	3.50	                6.65                 [1.9]
(8)	3.00	                4.00                 [1.333+]
(9)	3.00	                5.70                 [1.9]
(10)	2.37	                4.50                 [1.899+]
(11)	1.32	                2.50                 [1.894+]

Sec. 22. Flags manufactured or purchased for the use of executive agencies:

(a) Shall conform to the provisions of Part I of this order, except as may be 
otherwise authorized pursuant to the provisions of section 24, or except as 
otherwise authorized by the provisions of section 21, of this order.

(b) Shall conform to the provisions of section 21 of this order, except as may
be otherwise authorized pursuant to the provisions of section 24 of this order.

Sec. 23. The exterior dimensions of each union jack manufactured or purchased 
for executive agencies shall equal the respective exterior dimensions of the 
union of a flag of a size authorized by or pursuant to this order. The size of
the union jack flown with the national flag shall be the same as the size of 
the union of that national flag.

Sec. 24. (a) The Secretary of Defense in respect of procurement for the 
Department of Defense (including military colors) and the Administrator of 
General Services in respect of procurement for executive agencies other than 
the Department of Defense may, for cause which the Secretary or the 
Administrator, as the case may be, deems sufficient, make necessary minor 
adjustments in one or more of the dimensions or proportionate dimensions 
prescribed by this order, or authorize proportions or sizes other than those 
prescribed by section 3 or section 21 of this order.

(b) So far as practicable, (1) the actions of the Secretary of Defense under 
the provisions of section 24(a) of this order, as they relate to the various 
organizational elements of the Department of Defense, shall be coordinated, 
and (2) the Secretary and the Administrator shall mutually coordinate their 
actions under that section.

Sec. 25. Subject to such limited exceptions as the Secretary of Defense in 
respect of the Department of Defense, and the Administrator of General 
Services in respect of executive agencies other than the Department of 
Defense, may approve, all national flags and union jacks now in the 
possession of executive agencies, or hereafter acquired by executive agencies
under contracts awarded prior to the date of this order, including those so 
possessed or so acquired by the General Services Administration for 
distribution to other agencies, shall be utilized until unserviceable.

Part III - General Provisions

Sec. 31. The flag prescribed by Executive Order No. 10798 of January 3, 1959, 
shall be the official flag of the United States until July 4, 1960, and on that 
date the flag prescribed by Part I of this order shall become the official flag 
of the United States; but this section shall neither derogate from section 24 or 
section 25 of this order nor preclude the procurement, for executive agencies, 
or flags provided for by or pursuant to this order at any time after the date of 
this order.

Sec. 32. As used in this order, the term "executive agencies" means the 
executive departments and independent establishments in the executive branch of 
the Government, including wholly-owned Government corporations.

Sec. 33. Executive Order No. 10798 of January 3, 1959, is hereby revoked.

Attachment [Reproduction of essentials in ASCII art]

   |           D           |
   |G|H|H|H|H|H|H|H|H|H|H|G|
---+-----------------------+-------------------------+
  E|Blue     White Stars   |Red                      |
  -| *   *   *   *   *   * |                         |
  F|                       |-------------------------|
  -|   *   *  (*)  *   *   |White                    |
  F|          |K|          |                         |
  -| *   *   *   *   *   * |-------------------------|
  F|                       |Red                      |
  -|   *   *   *   *   *   |                         |
  F|                       |-------------------------|
 C-| *   *   *   *   *   * |White                    |
  F|                       |                         |
  -|   *   *   *   *   *   |-------------------------|
  F|                       |Red                      |
  -| *   *   *   *   *   * |                         |
  F|                       |-------------------------|
  -|   *   *   *   *   *   |White                    |
  F|                       |                         |
  -| *   *   *   *   *   * |-------------------------|
A E|                       |Red(Equal width as above)|
 --+-----------------------+-------------------------|
   |White                                            |
   |                                                 |
  -|-------------------------------------------------|
  L|Red                                              |
   |                                                 |
  -|-------------------------------------------------|
   |White                                            |
   |                                                 |
   |-------------------------------------------------|
   |Red                                              |
   |                                                 |
   |-------------------------------------------------|
   |White                                            |
   |                                                 |
   |-------------------------------------------------|
   |Red                                              |
   |                                                 |
---+-------------------------------------------------+
   |                       B                         |
[Things not clear from this ASCII art, but clear in the attachment:
*The diameter of a star is the diameter of a circle circumscribing it. 
*The center of a star is the center of the circle circimscribing it. 
*The stars themselves are regular pentagrams (pen-ta-gram n.: a star 
 with 5 points; formed by 5 straight lines between the vertices of a 
 pentagon and enclosing another pentagon) with one point pointing
 straight up
 ]
   
Standard proportions
A	Hoist (width) of flag	  1.0
B	Fly (length) of flag	  1.9
C	Hoist (width) of Union	0.5385 (7/13)
D	Fly (length) of Union 	0.76
E	                [7/130]	0.054   [Distance from top edge of union to center of 
                                  first row star. Also distance between last 
                                  row star and bottom edge of union]
F               	[7/130]	0.054  [Distance between line through centers of stars
                                  in one row and line through the next row. The next
                                  row means the one with a different number of stars]
G               [0.76/12]	0.063  [Distance from hoist of flag to center of first
                                  column star. Also distance between last column
                                  star and right edge of union]
H               [0.76/12]	0.063  [Distance between line through centers of stars
                                  in one column and line through the next column. The next
                                  column means the one with a different number of stars]
K	Diameter of star	     0.0616 
L	Width of stripe 	     0.0769 (1/13)

[The given E,F,G, and H are inconsistent, meaning that they do not add up to C and D
(2*E+8*F=0.540)!=(7/13=C)
(2*G+10*H=0.756)!=(0.76=D)
The figures in brackets do add up, and match the specs to the given significant figures]

To get even K in the fold

 | Rational | 2 | 3 | 5 | 13
-+----------+---+---+---+----
A|     1    | 0 | 0 | 0 |  0
B|  19/10   | 1 | 0 | 1 |  0
C|   7/13   | 0 | 0 | 0 |  1
D|  19/25   | 0 | 0 | 2 |  0
E|   7/130  | 1 | 0 | 1 |  1
F|   7/130  | 1 | 0 | 1 |  1
G|  19/300  | 2 | 1 | 2 |  0
H|  19/300  | 2 | 1 | 2 |  0
K|  77/1250 | 1 | 0 | 4 |  0

Counting all but K, you need to multiply A by 3900 to get 
              2   1   2    1

Run at +w7410 +h3900 (or integer multiples thereof) and all dimensions except K given above are integers
Run at +w185250 +h97500 (or integer multiples thereof) and all dimensions given above are integers

 | Rational | 3900   |  97500 
-+----------+--------+--------
A|     1    | 3900   |  97500
B|  19/10   | 7410   | 185250
C|   7/13   | 2100   |  52500 
D|  19/25   | 2964   |  74100
E|   7/130  |  210   |   5250
F|   7/130  |  210   |   5250
G|  19/300  |  247   |   6175
H|  19/300  |  247   |   6175
K|  77/1250 |  240.24|   6006

*/                          

//Star formulas from http://mathworld.wolfram.com/Pentagram.html

#macro Star(StarRadius)
  #local FormulaRho=sqrt((5-sqrt(5))/10);
  #local FormulaR=sqrt((25-11*sqrt(5))/10);
  #local RealRho=StarRadius;
//  #debug concat(str(RealRho,0,8),"\n")
  #local RealR=FormulaR/FormulaRho*RealRho;
//  #debug concat(str(RealR,0,8),"\n")
	
  #local I=0;
  #while(I<5)
    #local Theta=radians(I*72);
    <sin(Theta),cos(Theta)>*RealRho
    #local Theta=radians(I*72+36);
    <sin(Theta),cos(Theta)>*RealR
    #local I=I+1;
  #end
#end	

#declare FlagA=1.0;
#declare FlagB=1.9;
#declare FlagC=7/13;
#declare FlagD=0.76;
#declare FlagE=FlagC/10; //=7/130 =0.0538461
#declare FlagF=FlagE;    //     _
#declare FlagG=FlagD/12; //=0.063
#declare FlagH=FlagG;    //       ______
#declare FlagK=0.0616;   //1/K=16.233766
#declare FlagL=1/13;

//The specs still do not specify the exact colors to be used. 
//Color reference http://www.aperion.de/pantone2rgb3.htm
//#declare FlagRed=color rgb <222,0,54>/255; //Old Glory Red, PMS 193
//#declare FlagWhite=color rgb <1,1,1>;
//#declare FlagBlue=color rgb <0,0,173>/255; //Old Glory Blue, PMS 281

//Color reference http://en.wikipedia.com/wiki/Flag_of_the_United_States#Colors
#declare FlagRed=color rgb <191,10,48>/255; //Dark Red, 193 C
#declare FlagWhite=color rgb <1,1,1>;
#declare FlagBlue=color rgb <0,40,104>/255; //Navy Blue, 281 C

//These are just good looking colors that give a nice dark but still distinct blue and red
//#declare FlagRed=color rgb <192,0,0>/255; //#C00000
//#declare FlagWhite=color rgb <1,1,1>;
//#declare FlagBlue=color rgb <0,0,160>/255; //#0000A0

#local I=0;
#while(I<3)
  box {
    <FlagD,FlagA-FlagL*(I*2),0>,<FlagB,FlagA-FlagL*(I*2+1),0.01>
    pigment {color FlagRed}
    finish {ambient 1 diffuse 0}
  }
  box {
    <FlagD,FlagA-FlagL*(I*2+1),0>,<FlagB,FlagA-FlagL*(I*2+2),0.01>
    pigment {color FlagWhite}
    finish {ambient 1 diffuse 0}
  }
  box {
    <0,FlagA-FlagL*(I*2+7),0>,<FlagB,FlagA-FlagL*(I*2+8),0.01>
    pigment {color FlagWhite}
    finish {ambient 1 diffuse 0}
  }
  box {
    <0,FlagA-FlagL*(I*2+8),0>,<FlagB,FlagA-FlagL*(I*2+9),0.01>
    pigment {color FlagRed}
    finish {ambient 1 diffuse 0}
  }
  #local I=I+1;
#end		

box {
  <FlagD,FlagL*6,0>,<FlagB,FlagL*7,0.01>
  pigment {color FlagRed}
  finish {ambient 1 diffuse 0}
}

#local I=0;
#while(I<5)
  #local J=0;
  #while(J<6)
    prism {
      linear_sweep
      -0.02,0.02,10
      Star(FlagK/2)
      rotate -x*90
      translate <FlagG+J*FlagH*2,FlagA-(FlagE+I*FlagF*2),0>
      pigment {color FlagWhite}
      finish {ambient 1 diffuse 0}
    }
    #local J=J+1;
  #end
  #local I=I+1;
#end

#local I=0;
#while(I<4)
  #local J=0;
  #while(J<5)
    prism {
      linear_sweep
      -0.02,0.02,10
      Star(FlagK/2)
      rotate -x*90
      translate <FlagG+FlagH+J*FlagH*2,FlagA-(FlagE+FlagF+I*FlagF*2),0>
      pigment {color FlagWhite}
      finish {ambient 1 diffuse 0}
    }
    #local J=J+1;
  #end
  #local I=I+1;
#end

box {
  <0,FlagA,0>,<FlagD,FlagA-FlagC,0.01>
  pigment {color FlagBlue}
  finish {ambient 1 diffuse 0}
}

camera {
  orthographic
  up y*FlagA
  right x*FlagB
  location <FlagB/2,FlagA/2,-1>
  look_at <FlagB/2,FlagA/2,0>
}
